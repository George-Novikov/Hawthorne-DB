plugins {
    id 'java-library'
    id 'maven-publish'
    id 'signing'
}

group 'com.george-n'
version '1.0.1'
def artifactName = 'hawthorne'

java {
    sourceCompatibility = '1.8'
    withJavadocJar()
    withSourcesJar()
}

repositories {
    mavenCentral()
}

dependencies {
    api 'com.fasterxml.jackson.core:jackson-core:2.16.1'
    api 'com.fasterxml.jackson.core:jackson-databind:2.16.1'
    api 'com.fasterxml.jackson.core:jackson-annotations:2.16.1'
    api 'com.fasterxml.jackson.dataformat:jackson-dataformat-yaml:2.16.1'
    api 'com.fasterxml.jackson.datatype:jackson-datatype-jsr310:2.16.1'
    api 'jakarta.persistence:jakarta.persistence-api:3.0.0'
    api 'javax.persistence:javax.persistence-api:2.2'
    annotationProcessor fileTree(dir: 'src/main/resources/libs', include: ['*.jar'])
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.8.1'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.8.1'
    testImplementation 'org.mockito:mockito-core:4.11.0'
    testImplementation 'org.mockito:mockito-junit-jupiter:4.11.0'
}

sourceSets {
    test {
        java {
            srcDirs = ['src/test/java']
        }
    }
}

test {
    useJUnitPlatform()
}

tasks.named('jar'){
    manifest {
        attributes(
                'Implementation-Title': artifactName,
                'Implementation-Version': project.version
        )
    }
}

artifacts {
    archives javadocJar, sourcesJar
}

signing {
    sign configurations.archives
}

publishing {
    publications {
        maven(MavenPublication) {
            groupId = 'com.george-n'
            artifactId = 'hawthorne'
            version = '1.0.0'
            from components.java
            pom {
                name = 'Hawthorne'
                description = 'A Java library that provides a simple way to store and manage objects in a database-like tree structure without third-party software, using only the OS file system.'
                url = 'https://github.com/George-Novikov/Hawthorne-DB'
                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id = 'georgen'
                        name = 'George Novikov'
                        email = 'george.novikov.it@gmail.com'
                    }
                }
                scm {
                    connection = 'scm:git:git@github.com:George-Novikov/Hawthorne-DB.git'
                    developerConnection = 'scm:git:git@github.com:George-Novikov/Hawthorne-DB.git'
                    url = 'https://github.com/George-Novikov/Hawthorne-DB'
                }
            }
        }
    }
    repositories {
        maven {
            credentials {
                username = ossrhUsername
                password = ossrhPassword
            }
            url = "https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/"
        }
    }
}